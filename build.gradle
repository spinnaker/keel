/*
 * Copyright 2017 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  repositories {
    jcenter()
    gradlePluginPortal()
    maven { url "http://spinnaker.bintray.com/gradle" }
  }
  dependencies {
    classpath "com.netflix.spinnaker.gradle:spinnaker-dev-plugin:5.2.1"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$bootVersion"
    classpath "com.netflix.nebula:nebula-kotlin-plugin:$kotlinVersion"
    classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
  }
}

class JacksonAlignmentRule implements ComponentMetadataRule {
  void execute(ComponentMetadataContext ctx) {
    ctx.details.with {
      if (id.group.startsWith("com.fasterxml.jackson")) {
        // declare that Jackson modules all belong to the Jackson virtual platform
        belongsTo("com.fasterxml.jackson:jackson-platform:${id.version}")
      }
    }
  }
}

allprojects {
  apply plugin: "spinnaker.base-project"

  group = "com.netflix.spinnaker.keel"

  configurations.all {
    exclude group: "javax.servlet", module: "servlet-api"
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "org.slf4j", module: "slf4j-simple"
  }
}

subprojects {
  apply from: "$rootDir/gradle/kotlin.gradle"
  apply from: "$rootDir/gradle/junit5.gradle"
  apply from: "$rootDir/gradle/license.gradle"

  repositories {
    jcenter()
  }
  
  dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:$bootVersion")
    components.all(JacksonAlignmentRule)
  }

  configurations {
    ["compile", "runtime", "testCompile", "testRuntime"].each { scope ->
      getByName("${scope}Classpath") {
        resolutionStrategy.activateDependencyLocking()
      }
    }
  }

  test {
    testLogging {
      exceptionFormat = "full"
    }
  }
}

/**
 * Use this task with --write-locks to upgrade all the dependencies in one shot
 */
task allDependencies {
  evaluationDependsOnChildren()
  dependsOn subprojects.collect { it.tasks["dependencies"] }
}

defaultTasks ":keel-api:run"
